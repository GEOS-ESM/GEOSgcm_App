#!/bin/csh -f

#######################################################################
#             Experiment Specific Environment Variables
#######################################################################

setenv  ARCH   `uname`
setenv  SITE    {{ SITE }}

setenv  EXPID   {{ EXPID }}
setenv  EXPDIR  {{ EXPDIR }}
setenv  HOMDIR  {{ HOMDIR }}
setenv GEOSUTIL {{ GEOSUTIL }}

#######################################################################
#                       Parameters for Forecasts
#
#   FCST_TYPE: exact
#              Forecast using archived 21z Restarts.
#              Perform EXACT REPLAY for 6-hours to archived AGCM_IMPORT files.
#
#   FCST_TYPE: regular
#              Forecast using archived 21z Restarts.
#              Perform REGULAR REPLAY for 6-hours to archived ANA.ETA files
#
#   FCST_TYPE: cycled
#              Identical to FCST_TYPE: regular, except that a single 21z Restart
#              is used to generate the first forecast.  All other forecasts are
#              created by cycling restarts in conjunction with performing a
#              REGULAR REPLAY to archived ANA.ETA files.
#
#######################################################################

set    BDATE = $1
set    EDATE = $2
set FSEGMENT = `echo $3 | bc | xargs printf "%08i"`
@   FLOOP    = $3 + 1
@   FHRS     = $3 * 24
set     QSUB = $4

if( .$QSUB == . ) set QSUB = FALSE

set     NFMAX = 1


set                  NX   = `grep '^\s*NX:'                    $HOMDIR/AGCM.rc | cut -d: -f2`
set                  NY   = `grep '^\s*NY:'                    $HOMDIR/AGCM.rc | cut -d: -f2`
set           ANA_EXPID   = `grep '^\s*REPLAY_ANA_EXPID:'      $HOMDIR/AGCM.rc | cut -d: -f2`
set           FCST_TYPE   = `grep '^\s*REPLAY_MODE:'           $HOMDIR/AGCM.rc | cut -d: -f2`
set REPLAY_FILE_FREQUENCY = `grep '^\s*REPLAY_FILE_FREQUENCY:' $HOMDIR/AGCM.rc | cut -d: -f2`
set       MKIAU_FREQUENCY = `grep '^\s*MKIAU_FREQUENCY:'       $HOMDIR/AGCM.rc | cut -d: -f2`

if( .$REPLAY_FILE_FREQUENCY == '.' ) set REPLAY_FILE_FREQUENCY = 21600
if(       .$MKIAU_FREQUENCY == '.' ) set       MKIAU_FREQUENCY = $REPLAY_FILE_FREQUENCY

set FCST_LABL = "${FCST_TYPE}_RPLFRQ-${REPLAY_FILE_FREQUENCY}_ANA-${ANA_EXPID}"

if( $REPLAY_FILE_FREQUENCY != $MKIAU_FREQUENCY ) then
    set FCST_LABL = "${FCST_TYPE}_IAUFRQ-${MKIAU_FREQUENCY}_RPLFRQ-{$REPLAY_FILE_FREQUENCY}_ANA-${ANA_EXPID}"
endif

#######################################################################
#                   Create Forecast SubScripts
#######################################################################

     @ count  = 0
set     date  = $BDATE
while( $date <= $EDATE )

   @ NF = 1
   set     test  = $date
   while( $test <= $EDATE & $NF <= $NFMAX )
   set nymd = `$GEOSUTIL/post/tick $test 000000 86400`
   set test = $nymd[1]
   @ NF = $NF + 1
   end
   set nymde = `$GEOSUTIL/post/tick $test 000000 -86400`
   set nymde =  $nymde[1]

   @ NF = $NF - 1
   @ count = $count + 1
   echo $date $NF
   /bin/rm -f gcm_forecast_${FCST_LABL}.j${date}-${nymde}
   /bin/rm -f sedfile
   /bin/rm -f gcm_forecast.tmp
   /bin/cp -f $EXPDIR/forecasts/gcm_forecast.tmpl $EXPDIR/forecasts/gcm_forecast.tmp

if(! $?CYCLED ) then
   set cycled = .FALSE.
else
   set cycled = $CYCLED
endif

@ NPES = $NX * $NY

cat > sedfile << EOF
s?&cycled?$cycled?g
s?&DATE?$date?g
s?&NPES?$NPES?g
s?&NFMAX?$NF?g
s?&FCST_TYPE?$FCST_TYPE?g
s?&FCST_LABL?$FCST_LABL?g
s?&FSEGMENT?$FSEGMENT?g
s?FCSTOUT?$EXPDIR/forecasts/gcm_forecast_${FCST_LABL}.o${date}-${nymde}?g
s?&FLOOP?$FLOOP?g
s?&FHRS?$FHRS?g
EOF

sed -f sedfile $EXPDIR/forecasts/gcm_forecast.tmp > $EXPDIR/forecasts/gcm_forecast_${FCST_LABL}.j${date}-${nymde}
if( $QSUB != 'FALSE' )                        {{ BATCH_CMD }}  $EXPDIR/forecasts/gcm_forecast_${FCST_LABL}.j${date}-${nymde}

set   date = $test
end

/bin/rm -f sedfile
/bin/rm -f $EXPDIR/forecasts/gcm_forecast.tmp

